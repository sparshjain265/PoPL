%%

%eop EOF
%pure
%name Tiger

(* This section is for the terminal of the language *)
%term RECORD
        | EOF
        | ID of string
        | LEFTBRACE 
        | RIGHTBRACE
        | COMMA
        | SEMICOLON
        | COLON
        | LEFTBRACKET
        | RIGHTBRACKET
        | STATIC
        | LPARA
        | RPARA
        | INT 
        | BOOL 
        | CHAR 
        | VOID
        | IF
        | ELSE
        | WHILE
        | FOR 
        | RETURN 
        | BREAK 
        | CONTINUE 
        | EQUAL
        | PLUSEQUAL
        | MINUSEQUAL
        | MULTEQUAL
        | DIVEQUAL
        | INC
        | DEC 
        | OR
        | AND 
        | NOT 
        | LESSEQUAL
        | LESS 
        | GREATER 
        | GREATEREQUAL 
        | EQUALEQUAL
        | NOTEQUAL
        | PLUS 
        | MINUS 
        | MULT 
        | DIV 
        | MOD 
        | QUES
        | DASH 
        | STAR
        | DOT 
        | NUMCONST of string 
        | CHARCONST  of string 
        | TRUE 
        | FALSE
        | iff 
        | c 


(* The nonterminals of the language *)
%nonterm program of Ast.program
        | declarationList of Ast.declarationList
        | declaration of  Ast.declaration
        | recDeclaration of Ast.recDeclaration
        | varDeclaration of Ast.varDeclaration
        | scopedVarDeclaration of Ast.scopedVarDeclaration
        | varDeclList of Ast.varDeclList
        | varDeclInitialize of Ast.varDeclInitialize
        | varDeclID of Ast.varDeclID
        | scopedTypeSpecifier of Ast.scopedTypeSpecifier
        | typeSpecifier of Ast.typeSpecifier
        | returnTypeSpecifier of Ast.returnTypeSpecifier
        | funDeclaration of Ast.funDeclaration
        | params of Ast.params
        | paramList of Ast.paramList
        | paramTypeList of Ast.paramTypeList
        | paramIDList of Ast.paramIDList
        | paramID of Ast.paramID
        | statement of Ast.statement
        | compoundStmt of Ast.compoundStmt
        | localDeclarations of Ast.localDeclarations
        | statementList of Ast.statementList
        | expressionStmt of Ast.expressionStmt
        | selectionStmt of Ast.selectionStmt
        | iterationStmt of Ast.iterationStmt
        | returnStmt of Ast.returnStmt
        | breakStmt of Ast.breakStmt
        | continueStmt of Ast.continueStmt
        | expression of Ast.expression
        | simpleExpression of Ast.simpleExpression
        | andExpression of Ast.andExpression
        | unaryRelExpression of Ast.unaryRelExpression
        | relExpression of Ast.relExpression
        | sumExpression of Ast.sumExpression
        | term of Ast.term
        | unaryExpression of Ast.unaryExpression
        | factor of Ast.factor
        | mutable of Ast.mutable
        | immutable of Ast.immutable
        | call of Ast.call
        | args of Ast.args
        | argList of Ast.argList
        | constant of Ast.constant
    



%noshift EOF
%verbose 

(* Precedence increase in the downward direction *)
%left OR 
%left AND 
%left NOT 
%left LESSEQUAL LESS GREATER GREATEREQUAL EQUALEQUAL NOTEQUAL 
%left PLUS MINUS 
%left MULT DIV MOD 
%left QUES DASH STAR
%nonassoc iff 
%nonassoc ELSE 
%nonassoc c
%nonassoc LPARA 



(* The type that captures position in the input *)
%pos int

(* Grammar of the language *)
%%

(* =============================== Grammar ================================*)
program : declarationList                                       ( Ast.declL(declarationList) )

declarationList : declarationList declaration                   ( Ast.declList (declarationList , declaration) )
                | declaration                                   ( Ast.singleDecl (declaration) )

declaration : varDeclaration                                    (Ast.variableDeclaration (varDeclaration) ) 
            | funDeclaration                                    (Ast.functionDeclaration (funDeclaration) )
            | recDeclaration                                    (Ast.recordDeclaration (recDeclaration) )

(* ============================ record declaration ====================== *)
recDeclaration : RECORD ID LEFTBRACE localDeclarations RIGHTBRACE   (Ast.recordID (ID , localDeclarations))

(* ============================ variable declaration ===================== *)
varDeclaration : typeSpecifier varDeclList SEMICOLON                         (Ast.vDecl (typeSpecifier , varDeclList))

scopedVarDeclaration : scopedTypeSpecifier varDeclList  SEMICOLON          (Ast.sDecl (scopedTypeSpecifier , varDeclList) )

varDeclList : varDeclList COMMA varDeclInitialize                   (Ast.vList (varDeclList , varDeclInitialize))
             | varDeclInitialize                                    (Ast.vSingleDecl (varDeclInitialize))

varDeclInitialize : varDeclID                                       (Ast.declarationOnlyID (varDeclID) )
                | varDeclID COLON simpleExpression                  (Ast.declarationAssignment (varDeclID , simpleExpression))

varDeclID : ID                                                     (Ast.vID (ID))
            | ID LEFTBRACKET NUMCONST RIGHTBRACKET                  (Ast.arrayLike (ID , NUMCONST))

scopedTypeSpecifier : STATIC typeSpecifier                          (Ast.staticType (typeSpecifier))
                    | typeSpecifier                                 (Ast.simpleType (typeSpecifier))

typeSpecifier : returnTypeSpecifier                                 (Ast.ret (returnTypeSpecifier))
                (* | ID                                                 (Ast.RECTYPE (ID)) *)
returnTypeSpecifier : INT                                           (Ast.integer)
                    | BOOL                                          (Ast.boolean)
                    | CHAR                                          (Ast.character)


(* ================================== function declaration =========================== *)
funDeclaration : typeSpecifier ID LPARA params RPARA statement       (Ast.functionReturn (typeSpecifier , ID , params , statement))  
                | VOID ID LPARA params RPARA statement              (Ast.functionVoid (ID , params , statement))

params : paramList                                                  (Ast.parameterList (paramList))
        |                                                           (Ast.emptyParameter)

paramList : paramList SEMICOLON paramTypeList                       (Ast.pList (paramList , paramTypeList))                   
        | paramTypeList                                             (Ast.singleParam (paramTypeList))

paramTypeList : typeSpecifier paramIDList                           (Ast.parameter(typeSpecifier , paramIDList))
        
paramIDList : paramIDList COMMA paramID                             (Ast.listOfID (paramIDList , paramID)) 
            | paramID                                               (Ast.singleIDParameter (paramID))

paramID : ID                                                        (Ast.normalID (ID))
        | ID LEFTBRACKET RIGHTBRACKET                               (Ast.arrayID (ID))

(* =============================== statements ==================================== *)
statement : expressionStmt                                          (Ast.eStatement (expressionStmt)) 
            | compoundStmt                                          (Ast.cStatement (compoundStmt))
            | selectionStmt                                         (Ast.sStatement (selectionStmt))
            | iterationStmt                                         (Ast.iStatement (iterationStmt))
            | returnStmt                                            (Ast.rStatement (returnStmt))
            | breakStmt                                             (Ast.bStatement (breakStmt))
            | continueStmt                                           (Ast.conStatement (continueStmt))
                
compoundStmt : LEFTBRACE localDeclarations statementList RIGHTBRACE   (Ast.statementWithBrace (localDeclarations , statementList))

localDeclarations : localDeclarations scopedVarDeclaration          (Ast.declIn (localDeclarations , scopedVarDeclaration) ) 
            |                                                       (Ast.emptyDeclIn)

statementList : statementList statement                             (Ast.listOfStatements (statementList , statement)) 
            |                                                       (Ast.emptyListStatement)

expressionStmt : expression SEMICOLON                               (Ast.basicExpression (expression))
             | SEMICOLON                                            (Ast.semicolon )

selectionStmt : IF LPARA simpleExpression RPARA statement       %prec iff   (Ast.IF (simpleExpression , statement))  
            | IF LPARA simpleExpression RPARA statement ELSE statement  (Ast.IF_ELSE (simpleExpression , statement1 , statement2))

iterationStmt : WHILE LPARA simpleExpression RPARA statement         (Ast.WHILE(simpleExpression , statement))
        
returnStmt : RETURN SEMICOLON                                        (Ast.returnNoValue)
            | RETURN expression SEMICOLON                           (Ast.returnValue (expression))

breakStmt : BREAK SEMICOLON                                             (Ast.BREAK)
continueStmt : CONTINUE SEMICOLON                                       (Ast.CONTINUE)


(* ================================= EXPRESSION ======================================= *)
expression : mutable EQUAL expression                               (Ast.assign (mutable , expression))
            | mutable PLUSEQUAL expression                          (Ast.assignPlus (mutable , expression))
            | mutable MINUSEQUAL expression                         (Ast.assignMinus (mutable , expression))
            | mutable MULTEQUAL expression                          (Ast.assignMult (mutable , expression))
            | mutable DIVEQUAL expression                           (Ast.assignDiv (mutable , expression))
            | mutable INC                                           (Ast.increment (mutable))
            | mutable DEC                                           (Ast.decrement (mutable))
            | simpleExpression                                      (Ast.plainExpression (simpleExpression))

simpleExpression : simpleExpression OR andExpression                (Ast.or(simpleExpression , andExpression))
                | andExpression                                     (Ast.noOr (andExpression) )

andExpression : andExpression AND unaryRelExpression                (Ast.simpleAnd (andExpression , unaryRelExpression)) 
                | unaryRelExpression                                (Ast.uExpr (unaryRelExpression))

unaryRelExpression : NOT unaryRelExpression                         (Ast.not (unaryRelExpression))
                | relExpression                                      (Ast.rExpr (relExpression))

relExpression : sumExpression LESSEQUAL sumExpression                       (Ast.relExp (sumExpression , Ast.LTE , sumExpression ))    
                | sumExpression LESS sumExpression                           (Ast.relExp (sumExpression , Ast.LT , sumExpression ))
                | sumExpression GREATER sumExpression                       (Ast.relExp (sumExpression , Ast.GTE , sumExpression ))
                | sumExpression GREATEREQUAL sumExpression                  (Ast.relExp (sumExpression , Ast.GT , sumExpression ))
                | sumExpression EQUALEQUAL sumExpression                    (Ast.relExp (sumExpression , Ast.EQ , sumExpression ))
                | sumExpression NOTEQUAL sumExpression                      (Ast.relExp (sumExpression , Ast.NEQ , sumExpression ))
                | sumExpression                                              (Ast.noRel (sumExpression))

sumExpression : sumExpression PLUS term                                    (Ast.sumExp (sumExpression , Ast.PLUS , term)) 
                | sumExpression MINUS term                                  (Ast.sumExp (sumExpression , Ast.MINUS , term))
                | term                                                      (Ast.noSum (term))

term            : term MULT unaryExpression                                (Ast.multExp (term , Ast.MULT , unaryExpression)) 
                | term DIV unaryExpression                                 (Ast.multExp (term , Ast.DIV , unaryExpression))
                | term MOD unaryExpression                                 (Ast.multExp (term , Ast.MOD , unaryExpression))
                | unaryExpression                                          (Ast.noMult (unaryExpression))

unaryExpression : DASH unaryExpression                                    (Ast.uExp (Ast.DASH , unaryExpression)) 
                | STAR unaryExpression                                      (Ast.uExp (Ast.STAR , unaryExpression) )
                | QUES unaryExpression                                     (Ast.uExp (Ast.QUES , unaryExpression))
                | factor                                                   (Ast.noUnary (factor))

factor : immutable                                                         (Ast.immut (immutable))
        | mutable                                                          (Ast.mut (mutable))

mutable : ID                                          %prec c                     (Ast.mID (ID))
        | mutable LEFTBRACKET expression RIGHTBRACKET                      (Ast.mArray (mutable , expression ))
        | mutable DOT ID                                                    (Ast.mRecord (mutable,ID))

immutable : LPARA expression RPARA                                          (Ast.paranthesis(expression)) 
        | call                                                              (Ast.c (call))
        | constant                                                          (Ast.const (constant))

call : ID LPARA args RPARA                                                  (Ast.callArgs (ID , args))

args : argList                                                              (Ast.aList (argList))
        |                                                                    (Ast.emptyArg)

argList : argList COMMA expression                                            (Ast.aaList(argList , expression))
        | expression                                                            (Ast.oneArg (expression))

constant : NUMCONST                                                             (Ast.number(NUMCONST))
        | CHARCONST                                                             (Ast.charConst (CHARCONST))
        | TRUE                                                                  (Ast.trueValue)
        | FALSE                                                                 (Ast.falseValue)